plugins {
    id 'java'
    id 'jacoco'
    id 'groovy'
}

group = 'com.xq'

ext {
    springboot_version = '3.3.0'
    lombok_version = '1.18.30'
    junit_version = '5.11.3'
    slf4j_version = '1.7.36'
    testng_version = '7.9.0'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

//Repository configuration
repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repo.spring.io/release' }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// Custom sourceSets for System Integration Tests
sourceSets {
    e2eMain {
        java {
            srcDirs = ['e2e/main/java']
        }
        resources {
            srcDirs = ['e2e/main/resources']
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

    e2eTest {
        java {
            srcDirs = ['e2e/test/java']
        }
        resources {
            srcDirs = ['e2e/test/resources']
        }
        compileClasspath += sourceSets.e2eMain.output + sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.e2eMain.output + sourceSets.main.output + sourceSets.test.output
    }
}

//Dependencies configuration
dependencies {
    compileOnly("org.slf4j:slf4j-api:$slf4j_version")
    compileOnly("org.projectlombok:lombok:$lombok_version")

    annotationProcessor("org.projectlombok:lombok:$lombok_version")
    implementation('io.karatelabs:karate-core:1.5.1')
    testImplementation 'io.karatelabs:karate-core:1.5.1'

    // e2e dependencies
    e2eTestImplementation "org.testng:testng:${testng_version}"
    e2eTestImplementation 'org.yaml:snakeyaml:2.0'
    e2eTestImplementation sourceSets.main.output
    e2eTestImplementation sourceSets.e2eMain.output
}

project.extensions.create("unitTest", TestConfiguration)
project.extensions.create("intTest", TestConfiguration)
project.extensions.create("compTest", TestConfiguration)
// e2e test configuration extension
project.extensions.create("e2eTestConfig", TestConfiguration)

//Test configuration
tasks.named("test") {
    useJUnitPlatform()
    maxParallelForks = 4
    maxHeapSize = "1g"
    finalizedBy("jacocoTestReport")
}

// System Integration Test task with TestNG
tasks.register('e2eTest', Test) {
    description = 'Runs System Integration Tests using TestNG'
    group = 'verification'

    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath

    useTestNG() {
        def config = project.extensions.getByName('e2eTestConfig') as TestConfiguration

        // Suite file support (XML or YAML)
        if (config.suiteXmlFile) {
            suites(project.file(config.suiteXmlFile))
        }
        if (config.suiteYamlFile) {
            suites(project.file(config.suiteYamlFile))
        }

        // Groups configuration
        if (!config.includeGroups.isEmpty()) {
            includeGroups(config.includeGroups as String[])
        }
        if (!config.excludeGroups.isEmpty()) {
            excludeGroups(config.excludeGroups as String[])
        }

        // Parallel execution
        parallel = config.parallel
        threadCount = config.threadCount

        // Additional TestNG options
        preserveOrder = config.preserveOrder
        groupByInstances = config.groupByInstances
        useDefaultListeners = config.useDefaultListeners

        if (!config.listeners.isEmpty()) {
            listeners = config.listeners
        }
    }

    // Set verbose mode via system properties if requested
    if (project.extensions.getByName('e2eTestConfig').verbose) {
        systemProperty 'testng.verbose', '1'
    }

    // Test reporting
    reports {
        html.required = true
        junitXml.required = true
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = false
    }

    // Should run after standard tests but not part of check
    shouldRunAfter tasks.named('test')
}

// Configure custom output directory after project evaluation
project.afterEvaluate {
    def e2eTestTask = tasks.named('e2eTest', Test).get()
    def config = project.extensions.getByName('e2eTestConfig') as TestConfiguration
    if (config.outputDirectory) {
        e2eTestTask.reports.html.outputLocation = project.file(config.outputDirectory)
    }
}

tasks.named("jacocoTestReport") {
    dependsOn(tasks.named("test"))

    // Only include main sourceSet, exclude e2e
    sourceSets project.sourceSets.main

    // Explicitly exclude e2e from coverage
    classDirectories.setFrom(
        files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/e2e/**')
        })
    )
}